set positional-arguments

# Display help
help:
    just -l

# `codex`
codex *args:
    cargo run --bin codex -- "$@"

# `codex exec`
exec *args:
    cargo run --bin codex -- exec "$@"

# `codex tui`
tui *args:
    cargo run --bin codex -- tui "$@"

# Run the CLI version of the file-search crate.
file-search *args:
    cargo run --bin codex-file-search -- "$@"

# format code
fmt:
    cargo fmt -- --config imports_granularity=Item

fix *args:
    cargo clippy --fix --all-features --tests --allow-dirty "$@"

install:
    rustup show active-toolchain
    cargo fetch

# Run the MCP server
mcp-server-run *args:
    cargo run -p codex-mcp-server -- "$@"

# Safe sync & merge from upstream without history rewrites.
# Usage:
#   just sync                       # default
#   just sync -- --dry-run          # pass-through flags to script
#   just sync -- --help             # show script help
sync *args:
    scripts/safe_sync_merge.sh "$@"

# Update the resolver help golden snapshot (normalized for CRLF/blank lines)
update-resolver-golden:
    LC_ALL=C LANG=C bash scripts/resolve_safe_sync.sh --help | tr -d '\r' | awk 'NF{print $0}' ORS='\n' > docs/golden/resolver_help.txt
